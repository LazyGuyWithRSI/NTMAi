#include "zscript/enemies/zombieman.txt"
#include "zscript/enemies/shotgunguy.txt"
#include "zscript/enemies/chaingunguy.txt"
#include "zscript/enemies/imp.txt"
#include "zscript/enemies/demon.txt"
#include "zscript/enemies/lostsoul.txt"
#include "zscript/enemies/german.txt"

class NTM_Enemy : Actor {
	int ticsPerChase;
	
	property TicsPerChase: ticsPerChase;
	
	Default {
		Scale 0.666;
		Monster;
		
		+FloorClip
		+DontCorpse
	}
	
	States {
		See:
			#### # 0 {
				switch (sv_ntm_enemyMove) {
					case 0:
						SetStateLabel("DukeSee");
					break;
					
					case 1:
						SetStateLabel("BloodSee");
					break;
					
					case 2:
						SetStateLabel("SWSee");
					break;
				}
			}
			
			Stop;
	}
	
	// NTMAi monster actions
	action void A_NTM_DukeChase() {
		A_Chase();
		
		// Take an extra step if we didn't start attacking
		if (curState != FindState("Melee") && curState != FindState("Missile")) {
			A_Chase();
		}
	}
	
	action void A_NTM_BloodChase() {
		if (CheckMove(pos.xy)) {
			bool noTeleport = bNoTeleport;
			Vector3 oldPos = pos;
			
			bNoTeleport = true;
			vel.xy *= 0.8;
			A_Chase();
			SetOrigin(oldPos, true);
			A_Recoil(speed * -0.16);
			bNoTeleport = noTeleport;
		} else {
			A_Chase();
		}
	}
	
	action void A_NTM_SWChase() {
		if (CheckMove(pos.xy)) {
			int tpc = NTM_Enemy(self).ticsPerChase;
			
			speed /= tpc;
			A_Chase();
			speed *= tpc;
		} else {
			A_Chase();
		}
	}
	
	action void A_NTM_SWChase2() {
		int tpc = NTM_Enemy(self).ticsPerChase;
		
		speed /= tpc;
		A_Chase(null, null, CHF_NoPlayActive | CHF_NoDirectionTurn | CHF_DontTurn);
		speed *= tpc;
	}
	
	action void A_NTM_PreDeath() {
		height = default.Height;
		bSolid = true;
		bShootable = true;
	}
	
	action void A_NTM_Death() {
		A_Scream();
		height = GetDeathHeight();
		bCorpse = true;
		bSolid = false;
		bShootable = false;
	}
	
	action void A_NTM_XDeath() {
		A_XScream();
		height = GetDeathHeight();
		bCorpse = true;
		bSolid = false;
		bShootable = false;
	}
	
	action void A_NTM_PainEnable() {
		painChance = default.painChance;
	}
	
	action void A_NTM_PainDisable() {
		painChance = 0;
	}
}